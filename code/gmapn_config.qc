/*
    gmapn_config.qc

    Copyright (C) 2007 Mark Olsen

    This program is free software; you can redistribute it and/or
    modify it under the terms of the GNU General Public License
    as published by the Free Software Foundation; either version 2
    of the License, or (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

    See the GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to:

        Free Software Foundation, Inc.
        59 Temple Place - Suite 330
        Boston, MA  02111-1307, USA

*/

/* Ugly, ugly, ugly */
float gmapn_config_version;

void gmapn_config_update()
{
	if (stof(infokey(world, "gmapn_config_version")) == gmapn_config_version)
	{
		gmapn_init_entities();
		gmapn_update_defaults_rules();
		remove(self);
		return;
	}

	self.nextthink = time + 0.001;
};

void gmapn_config_load()
{
	dbgprintln("=== gmapn_config_load() ===");

	string g_game_mode = getkey(game_mode);
	string str;
	entity e;

	//localcmd("exec configs/default.cfg\n");
	getkey_parseconfig("cfgs/default.conf");

	str = strcat("cfgs/rules/", g_game_mode);
	str = strcat(str, ".conf");
	//localcmd(str);
	getkey_parseconfig(str);

	str = strcat("cfgs/maps/", mapname);
	str = strcat(str, "/default.conf");
	//localcmd(str);
	getkey_parseconfig(str);

	str = strcat("cfgs/maps/", mapname);
	str = strcat(str, "/");
	str = strcat(str, g_game_mode);
	str = strcat(str, ".conf");
	//localcmd(str);
	getkey_parseconfig(str);

	if (gmapn_config_version == 0)
		gmapn_update_defaults_start();
	
	gmapn_config_version+= 1;
	if (gmapn_config_version == 65536)
		gmapn_config_version = 0;

	str = strcat("localinfo gmapn_config_version ", ftos(gmapn_config_version));
	str = strcat(str, "\n");
	localcmd(str);

	localcmd(strcat("serverinfo config ", g_game_mode, "\n"));

	gmapn_serverstate = gmapn_serverstate_prewar;

	/* We need to wait for the server to actually load the config... */
	e = spawn();
	e.nextthink = time + 0.001;
	e.think = gmapn_config_update;
	e.health = gmapn_config_version;
};

void gmapn_config_exec(string cfg)
{
	filestream fs = fopen(cfg, FILE_READ);
	string line;
	
	if (fs < 0)
	{
		dprint("couldn't exec config ");
		dprint(cfg);
		dprint("\n");
		return;
	}

	while ((line = fgets(fs)))
	{
		localcmd(line);
		localcmd("\n");
	}
}
